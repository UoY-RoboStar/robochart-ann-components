--SETTING UP FOR MNIST, 10X10, 
--SEMANTIC CONSTANTS--
--MNIST is for handwriting data. 
--ASSUMPTIONS: Input order is FIXED. WE HAVE DIFFERNET LOGIC, JUST COMPARE WEIGHTS, BECAUSE FIRST LAYER INPUT IS EQUAL. Add to specify other types of input
--We need to add for every layer, the ordering, and not only the weight order, but if the input, for the node, is not max, and other input is, it WORKS. 

--IT WORKS, it does force one inactive, and two nodes to active. 
--Deadlocks with 10 nodes, 9? 1 layer, is it just about if you have one layer? 
--It might be, 
include "mnist_params.csp"
insize = 10
layerstructure = <10, 10>
layerNo = #layerstructure 
maxSize = 10
maxLayerSize = 10

--Why can't it evaluate anymore, being slow? Not because of couldnodemax or evaluation functions, why is it? 

--CHANNELS AND DATA TYPES: 

--Context channel: 

core_real = { -1..1}

--Decisions: 

datatype decisions = Zer | One | Two | Three | Four | Five | Six | Seven | Eight | Nine

channel context_input:{1..insize}.core_real

--We expect a single output of all possible decisions, for every trace. 
channel context_output:Set(decisions)

--ZERO IS NEVER TRANSMITTED, IT IS CALCULATED. 
--Zero is still useful, it means we can ignore those values, with Inactive, and if its inactive, its negative.
datatype InternalPhases = Zero | InActive | Active
--The input, uncertain means active. 
datatype Phases = active | inactive
datatype IsMax = Max | Not

channel NodePhase:{0..layerNo}.{1..maxSize}.Phases
--To communicate if the node can be the maximum of this layer or not.
channel IsNodeMax:{1..layerNo}.{1..maxLayerSize}.IsMax

channel end 


--ASSUME WORST CASE UNCERTAINTY, that, for us, is 
--Active or uncertain DEALT WITH IN THE SAME WAY BY EDGE LOGIC. 
EdgeLogic(input_phase, weight, first) = 
	if(first == true) then 
		if(input_phase == inactive)
			then 
				if(weight == Active)
					then 
						InActive
					else
						Active
			else
				if(weight == InActive)
					then
						InActive
					else
						Active
	else 
		if(input_phase == inactive)
			then 
				Zero
			else
				if(weight == Active)
					then 
						Active
					else
						InActive


--Comparison operators, 
--This is TRUE if w1 < w2, means that w1 appears SECOND in the list. Assume the list is HIGHEST TO LOWEST.
LayerWiseWeightOrder(layer, input, w1, w2) = IterateList(layerwise_weight_order(layer, input), w1, w2)
NodeWiseWeightOrder(layer, node, w1, w2) = IterateList(nodewise_weight_order(layer, node), w1, w2)


extractPosWeights(layer, node) = {i | i <- {1..layerSize(layer-1)}, extract_weights(layer, node, i) == InActive}
extractNegWeights(layer, node) = {i | i <- {1..layerSize(layer-1)}, extract_weights(layer, node, i) == Active}

--This is under ASSUMPTION THAT ALL INPUTS ARE THE SAME, THAT WE HAVE FOR THE INPUT LAYER. 
isInputLayerActive(_, _, <>, _) = True
isInputLayerActive(layer, node, negIndicies, posSet) = 
	if(card({ pos_i | pos_i <- posSet, NodeWiseWeightOrder(layer, node, head(negIndicies), pos_i)  }) > 0) 
		then 
			isInputLayerActive(layer, node, tail(negIndicies), 
				diff(posSet, {getLowestNodeIndex(layer, node, { pos_i | pos_i <- posSet, NodeWiseWeightOrder(layer, node, head(negIndicies), pos_i)  })}))
		else 
			False

isHiddenLayerActive(_, _, <>, _, _) = True
isHiddenLayerActive(layer, node, negIndicies, posSet, maxSeq) = 
	if(card({ pos_i | pos_i <- posSet, NodeWiseWeightOrder(layer, node, head(negIndicies), pos_i) and
				   extract_sequence(head(negIndicies), maxSeq) == Not and 
				   extract_sequence(pos_i, maxSeq) == Max }) > 0) 
		then 
			isInputLayerActive(layer, node, tail(negIndicies), 
				diff(posSet, {getLowestNodeIndex(layer, node, { pos_i | pos_i <- posSet, NodeWiseWeightOrder(layer, node, head(negIndicies), pos_i)  })}))
		else 
			False

IterateList(<>, w1, w2) = False
IterateList(l, w1, w2) = 
	if (head(l) == w1) 
		then
			False
	else if (head(l) == w2) 
		then 
			True 
	else 
		IterateList(tail(l), w1, w2)

--Get highest index FAILS if its 0, runs forever. weight order sort is INFINITE LOOP.s
getHighestIndex(layer, input, ISet) = 
	if(card(ISet) > 0) 
		then
			extract_sequence(card(ISet), weightOrderSort(layer, input, seq(ISet)))
		else
			0
			
getLowestNodeIndex(layer, node, ISet) = 
	if(card(ISet) > 0) 
		then
			head(nodeWiseSort(layer, node, seq(ISet)))
		else
			0

nodeWiseSort(layer, node, seqI) = 
	if(#seqI <= 1) then
		seqI
	else
		
		nodeWiseMerge(
			layer, node, 
			nodeWiseSort(layer, node, <extract_sequence(i, seqI) | i <- <1..(#seqI)/2>>),
			nodeWiseSort(layer, node, <extract_sequence(i, seqI) | i <- <(#seqI)/2+1..#seqI>>)
			)

nodeWiseMerge(layer, node, left, right) = nodeWiseMergeF(layer, node, left, right, <>)

nodeWiseMergeF(layer, node, <>, <>, result) = result
nodeWiseMergeF(layer, node, left, <>, result) = nodeWiseMergeF(layer, node, tail(left), <>, result^<head(left)>)
nodeWiseMergeF(layer, node, <>, right, result) = nodeWiseMergeF(layer, node, <>, tail(right), result^<head(right)>)
nodeWiseMergeF(layer, node, left, right, result) = 
	if(NodeWiseWeightOrder(layer, node, head(left), head(right)) == True)
		then 
			nodeWiseMergeF(layer, node, tail(left), right, result^<head(left)>)
	else
		nodeWiseMergeF(layer, node, left, tail(right), result^<head(right)>)
		


weightOrderSort(layer, input, seqI) = 
	if(#seqI <= 1) then
		seqI
	else
		
		merge(
			layer, input, 
			weightOrderSort(layer, input, <extract_sequence(i, seqI) | i <- <1..(#seqI)/2>>),
			weightOrderSort(layer, input, <extract_sequence(i, seqI) | i <- <(#seqI)/2+1..#seqI>>)
			)

merge(layer, input, left, right) = mergeF(layer, input, left, right, <>)

mergeF(layer, input, <>, <>, result) = result
mergeF(layer, input, left, <>, result) = mergeF(layer, input, tail(left), <>, result^<head(left)>)
mergeF(layer, input, <>, right, result) = mergeF(layer, input, <>, tail(right), result^<head(right)>)
mergeF(layer, input, left, right, result) = 
	if(LayerWiseWeightOrder(layer, input, head(left), head(right)) == True)
		then 
			mergeF(layer, input, tail(left), right, result^<head(left)>)
	else
		mergeF(layer, input, left, tail(right), result^<head(right)>)
		
countActive(<>) = 0
countActive(l) = if(head(l) == Active) then (countActive(tail(l))+1) else (countActive(tail(l)))

countInActive(<>) = 0
countInActive(l) = if(head(l) == InActive) then (countInActive(tail(l))+1) else (countInActive(tail(l)))


InActivation(l,n,edge_results, firstLayer, maxSeq) = 
	if(countActive(edge_results) > 0)
		then
			if(firstLayer)
			then
				isInputLayerActive(l, n, seq(extractPosWeights(l, n)), extractNegWeights(l,n))
			else
				isHiddenLayerActive(l, n, seq(extractPosWeights(l, n)), extractNegWeights(l,n), maxSeq)
		else
			True
	
			
Activation(l,n,edge_results,firstLayer,maxSeq) = 
	if(countInActive(edge_results) > 0)
		then
			if(firstLayer)
				then
					isInputLayerActive(l, n, seq(extractNegWeights(l, n)), extractPosWeights(l,n))
				else
					isHiddenLayerActive(l, n, seq(extractNegWeights(l, n)), extractPosWeights(l,n), maxSeq)
		else
			True
	
			
Node(layer, node, index) = let
	C(layer, node, 0, edge_results, maxSeq) = 
		( ( (extract_biases(layer,node) == Active) & ( 
				NodePhase.layer.node!active -> SKIP ) 
			)
		[]
		(extract_biases(layer,node) == InActive) & (	
			((InActivation(layer, node, edge_results, (layer==1), maxSeq) == True) & (
				NodePhase.layer.node!inactive -> SKIP )
			)
			)
			[]
			((InActivation(layer, node, edge_results, (layer==1), maxSeq) == False) & (
				NodePhase.layer.node!active -> SKIP)
			)
			) 
			;
			( (couldNodeMax(layer, node, edge_results) == False) & (
				IsNodeMax.layer.node!Not -> SKIP
			)
			[]
			(couldNodeMax(layer, node, edge_results) == True) & (
				IsNodeMax.layer.node!Max -> SKIP
			)
			)
			
			
	C(layer, node, index, edge_results, maxSeq) = 
		((layer == 1) & NodePhase.(layer-1).index?edge_phase -> C(layer, node, (index-1), <EdgeLogic(edge_phase, extract_weights(layer, node, index), (layer==1))>^edge_results, <>))
		[]
		((layer != 1) & NodePhase.(layer-1).index?edge_phase -> IsNodeMax.(layer-1).index?isMax -> 
			C(layer, node, (index-1), <EdgeLogic(edge_phase, extract_weights(layer, node, index), (layer==1))>^edge_results, <isMax>^maxSeq))
	
	within 
		C(layer, node, index, <>, <>)

HiddenLayer(layer, size, inputSize) = 
	(layer == 1) & ( ([| {| NodePhase.(layer-1) |} |] i: {1..size} @ Node(layer, i, inputSize)) )
	[]
	(layer != 1) & ( ([| {| NodePhase.(layer-1), IsNodeMax.(layer-1) |} |] i: {1..size} @ Node(layer, i, inputSize)) )

	
HiddenLayers =
	(layerNo == 1) & (HiddenLayer(1, layerSize(1), layerSize(0))) 
	[]
	(layerNo > 1) & (
	(HiddenLayer(1, layerSize(1), layerSize(0))) 
	[| {| NodePhase.1, IsNodeMax.1 |} |]
	(
	|| i : {2..layerNo} @
		[ {| NodePhase.(i-1), NodePhase.i, IsNodeMax.(i-1), IsNodeMax.(i) |} ] 
		HiddenLayer(i, layerSize(i), layerSize(i-1))  
	)
	)
		
InputLayer = 
	(; i : <0..insize-1> @ ( NodePhase.0.(insize-i).active -> SKIP ))
	
--FOR MNIST, the handwriting recognition.
context_translation(1) = Zer
context_translation(2) = One
context_translation(3) = Two
context_translation(4) = Three
context_translation(5) = Four
context_translation(6) = Five
context_translation(7) = Six
context_translation(8) = Seven
context_translation(9) = Eight
context_translation(10) = Nine
		
ActiveIndicies(results) = {i | i <- {1..#results}, extract_sequence(i, results) == Active}


couldNodeMax(l, n, results) = 
	if ( not member(True, { n == head(layerwise_weight_order(l, i)) | i <- {1..layerSize(l-1)}, extract_sequence(i, results) == Active})
		and 
		head(layerwise_bias_order(l)) != n ) 
		then
			False
		else 
			True

OutputLayerEdge(result) = 
if(result == active)
	then
		Active
	else
		InActive

--Doesn't use the max value, but store it for completeness.
OutputLayer = let	
	C(0, network_results) = 
		(card({ context_translation(i) | i <- {1..#network_results}, couldNodeMax((layerNo+1), i, network_results) == True}) == 0) & (
			context_output!decisions -> SKIP
			)
		[]
		(card({ context_translation(i) | i <- {1..#network_results}, couldNodeMax((layerNo+1), i, network_results) == True}) > 0) & (
			context_output!{context_translation(i) | i <- {1..#network_results}, couldNodeMax((layerNo+1), i, network_results) == True} -> SKIP
			)
	C(index, network_results) = NodePhase.layerNo.index?output -> IsNodeMax.layerNo.index?isMax -> C((index-1), <OutputLayerEdge(output)>^network_results)
										
	within 
		C(layerSize(layerNo), <>)

--Better if the output interpreter inside the main function, needs to be because of the recursion.
ANN = ( (InputLayer [| {| NodePhase.0 |} |] HiddenLayers)  
		[| {| NodePhase.layerNo, IsNodeMax.layerNo |} |] 
	   OutputLayer) 

assert ANN :[deadlock-free]
assert HiddenLayers :[deadlock-free]

assert OutputLayer:[deadlock-free]

--HELPER FUNCTIONS: 
-- Extraction Functions, because random access not implemented in CSPM, implemented as lists not a type of function --

extract_sequence(1, sequence) = head(sequence)
extract_sequence(index, sequence) = extract_sequence((index-1), tail(sequence))

layerSize(0) = insize
layerSize(layer) = extract_sequence(layer, layerstructure)

extract_weights(layer, node, index) = 
	extract_sequence(index, 
		(extract_sequence(node, 
			(extract_sequence(layer, weights)))))
--Extract weights of node
extract_biases(layer, node) = 
	(extract_sequence(node, 
		(extract_sequence(layer, biases))))
										
